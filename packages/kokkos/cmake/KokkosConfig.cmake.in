# - Config file for the Kokkos package
# It defines the following variables
#  Kokkos_INCLUDE_DIRS - include directories for Kokkos
#  Kokkos_LIBRARIES    - libraries to link against

# Compute paths
@PACKAGE_INIT@

GET_FILENAME_COMPONENT(Kokkos_CMAKE_DIR "${CMAKE_CURRENT_LIST_FILE}" PATH)
SET(Kokkos_INCLUDE_DIRS "@CONF_INCLUDE_DIRS@")

INCLUDE("${Kokkos_CMAKE_DIR}/KokkosTargets.cmake")


# These are IMPORTED targets created by KokkosTargets.cmake
SET(Kokkos_LIBRARY_DIRS @INSTALL_LIB_DIR@)
SET(Kokkos_LIBRARIES @KOKKOS_LIBRARIES@)
SET(Kokkos_SEPARATE_LIBS @KOKKOS_SEPARATE_LIBS@)
SET(Kokkos_TPL_LIBRARIES @KOKKOS_LIBS@)
SET(Kokkos_ENABLE_Cuda   @KOKKOS_ENABLE_CUDA@)
SET(Kokkos_ENABLE_OpenMP @KOKKOS_ENABLE_OPENMP@)
SET(Kokkos_ENABLE_Pthread @KOKKOS_ENABLE_PTHREAD@)
SET(Kokkos_ENABLE_Serial @KOKKOS_ENABLE_SERIAL@)

IF (${Kokkos_FIND_VERSION_MINOR})
  IF (${Kokkos_FIND_VERSION_MINOR} LESS ${Kokkos_VERSION_MINOR})
    FOREACH(LIB ${Kokkos_LIBRARIES})
      set_property(TARGET ${LIB} APPEND PROPERTY
         INTERFACE_COMPILE_DEFINITIONS "KOKKOS_ENABLE_DEPRECATED_CODE"
      )
    ENDFOREACH()
  ENDIF()
ENDIF()

FUNCTION(TARGET_LINK_KOKKOS TARGET)
CMAKE_PARSE_ARGUMENTS(
  PARSE
  "PRIVATE;PUBLIC;INTERFACE"
  ""
  ""
  ${ARGN}
)
FOREACH(LIB ${Kokkos_LIBRARIES})
  GET_TARGET_PROPERTY(PROP ${LIB} TYPE)
  SET(LINK_TYPE)
  IF (${PROP} STREQUAL "INTERFACE_LIBRARY")
    #if an interface library, you MUST use link interface
    SET(LINK_TYPE "INTERFACE")
  ELSEIF(PARSE_PUBLIC)
    SET(LINK_TYPE "PUBLIC")
  ELSEIF(PARSE_PRIVATE)
    SET(LINK_TYPE "PRIVATE")
  ELSEIF(PARSE_INTERFACE)
    SET(LINK_TYPE "INTERFACE")
  ENDIF()
  TARGET_LINK_LIBRARIES(${TARGET} ${LINK_TYPE} ${ARGN} ${LIB})
ENDFOREACH()
ENDFUNCTION(TARGET_LINK_KOKKOS)

#Find dependencies
INCLUDE(CMakeFindDependencyMacro)
SET(FOUND_HPX_DIR @HPX_DIR@)
SET(FOUND_HPX_ROOT @HPX_ROOT@)
#figure out if we found HPX using root or dir
#set the search path accordingly, unless it got overriden
IF (NOT DEFINED HPX_ROOT AND NOT DEFINED HPX_DIR)
  IF (FOUND_HPX_DIR)
    SET(HPX_DIR ${FOUND_HPX_DIR})
  ELSEIF(FOUND_HPX_ROOT)
    SET(HPX_ROOT ${FOUND_HPX_ROOT})
  ENDIF()
ENDIF()
SET(KOKKOS_ENABLE_HPX @KOKKOS_ENABLE_HPX@)
IF (KOKKOS_ENABLE_HPX)
  FIND_DEPENDENCY(HPX)
ENDIF()

